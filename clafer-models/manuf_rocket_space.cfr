abstract Facility
    machines
        laser_cutter?
        pressure_chamber?
        cure_box?
        waterjet_cutter?
        cnc_mill?
        mill?
        sander?
        buff?

    tooling
        molds?
            circular?
            fin_mold?
            nosecone?
                ogive?
                parabolic?
                conical?
            [Facility.machines.sander && Facility.machines.buff] // molds made from fiberglass
            [Facility.machines.mill && Facility.machines.pressure_chamber] //molds made from fiberglass
        mandrels?
            diam_6in?
            diam_4in?

abstract Rocket
    body
        xor connections
            friction
            epoxy
                [no Rocket.material.aluminum] // does not work well on metal
            screws
                [Rocket.material.aluminum => Facility.machines.mill] // need to drill holes
    fins
        xor type
            trapezoidal
            elliptical
                [Facility.machines.cnc_mill] // Complicated shape hard to do by hand
            freeform

        xor profile
            flat
                [Rocket.material.carbon_fiber => Facility.machines.waterjet_cutter || Facility.machines.laser_cutter] // can either laser cut or water jet
                [Rocket.material.aluminum => Facility.machines.waterjet_cutter] // can't laser cut most aluminum
                [Rocket.material.fiberglass || Rocket.material.carbon_fiber => Facility.tooling.molds.fin_mold]
            subsonic
                [Facility.machines.mill] // to cut the leading and trailing edge
            supersonic
                [Facility.machines.mill] // to cut the leading and trailing edge

    nosecone
        xor type
            parabolic
                [Mission.max_flight_regime.subsonic]
            conical
                [Mission.max_flight_regime.transonic || Mission.max_flight_regime.subsonic]
            ogive


    xor material
        fiberglass
            [Facility.machines.pressure_chamber && Facility.tooling.mandrels] // need cure box and mandrel
            [Facility.machines.mill || Facility.tooling.molds.fin_mold] //fins can be molded or cut
            [Facility.machines.sander && Facility.machines.buff]
        carbon_fiber
            [Facility.machines.cure_box]
            [Facility.machines.waterjet_cutter || Facility.machines.laser_cutter]
        aluminum
            [Facility.machines.waterjet_cutter && Facility.tooling.molds.circular]

    xor surface_finish
        unfinished
            [no Rocket.material.fiberglass] //safety hazard
        regular_paint
        smooth_paint
            [Facility.machines.sander] // material below needs to be smooth
        polished
            [Facility.machines.sander && Facility.machines.buff] //smooth material and buff gloss coat


abstract Mission
    xor max_flight_regime
        subsonic
            [no Rocket.surface_finish.polished]
            [no Rocket.fins.profile.supersonic]
        transonic
            [no Rocket.surface_finish.unfinished && no Rocket.surface_finish.regular_paint && no Rocket.fins.profile.subsonic && no Rocket.fins.profile.flat]
        supersonic
            [Rocket.surface_finish.polished && Rocket.fins.profile.supersonic]
            [no Rocket.body.connections.friction]
    xor altitude
        low
        medium
            [Mission.max_flight_regime.transonic || Mission.max_flight_regime.supersonic]
        high
            [Mission.max_flight_regime.supersonic]

TeamRocket: Rocket
    [no surface_finish.regular_paint]
    //[body.connections.epoxy]
    //[fins.profile.flat && fins.type.trapezoidal]
    //[nosecone.type.parabolic]

NSL: Mission
    [max_flight_regime.subsonic && no Rocket.material.aluminum]
    [altitude.low]



/*
Perfect_Shop : Facility
    [machines.mill]
    [machines.cnc_mill]
    [machines.waterjet_cutter]
    [machines.laser_cutter]
    [machines.pressure_chamber]
    [machines.sander]
    [machines.buff]
    [machines.cure_box]
    [tooling.mandrels.diam_4in && tooling.mandrels.diam_6in]
    [tooling.molds.circular]
    [tooling.molds.fin_mold]
    [tooling.molds.nosecone.ogive && tooling.molds.nosecone.conical && tooling.molds.nosecone.parabolic]



StartupShop : Facility
    [machines.mill]
    [machines.cnc_mill]
    [machines.sander]
    [machines.buff]

LipscombShop : Facility
    [machines.mill]
    [machines.cnc_mill]
    [no machines.waterjet_cutter]
    [machines.laser_cutter]
    [machines.pressure_chamber]
    [machines.sander]
    [machines.buff]
    [no machines.cure_box]
    [no tooling.mandrels.diam_4in && tooling.mandrels.diam_6in]
    [no tooling.molds.circular]
    [no tooling.molds.fin_mold]
    [no tooling.molds.nosecone.ogive && no tooling.molds.nosecone.conical && tooling.molds.nosecone.parabolic]
*/
VanderbiltShop : Facility
    //facility constraints
    [machines.mill]
    [no machines.cnc_mill]
    [machines.waterjet_cutter]
    [no machines.laser_cutter]
    [machines.pressure_chamber]
    [machines.sander]
    [machines.buff]
    [machines.cure_box]
    [tooling.mandrels.diam_4in && tooling.mandrels.diam_6in]
    [tooling.molds.circular]
    [no tooling.molds.fin_mold]
    [tooling.molds.nosecone.ogive && tooling.molds.nosecone.conical && no tooling.molds.nosecone.parabolic]
